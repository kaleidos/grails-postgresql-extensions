buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        mavenCentral()
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"

        classpath 'net.saliman:gradle-cobertura-plugin:2.2.7'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

plugins {
    id "io.spring.dependency-management" version "0.5.2.RELEASE"
    id "com.jfrog.bintray" version "1.2"
}

version '4.6.10'
group 'org.grails.plugins'

apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'org.grails.grails-plugin'
apply from:'https://raw.githubusercontent.com/grails/grails-profile-repository/master/profiles/plugin/templates/grailsCentralPublishing.gradle'
apply from:'https://raw.githubusercontent.com/grails/grails-profile-repository/master/profiles/plugin/templates/bintrayPublishing.gradle'

apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'


ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

cobertura {
    coverageExcludes = ['.*BootStrap.*', '.*test.*']
    coverageFormats = ['html', 'xml']
}

jar {
    exclude 'test/**'
    exclude 'UrlMappings.groovy'
    exclude 'error.gsp'
    exclude 'messages.properties'
    exclude 'hibernate/**'
    exclude 'layouts/**'
    exclude 'spring/**'

    doFirst {
        File pd = new File(buildDir, 'classes/main/META-INF/grails-plugin.xml')
        pd.text = pd.text.readLines().findAll { ! it.contains('<resource>test.') }.join('\n')
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}


dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    provided 'org.grails:grails-core'
    provided 'org.grails.plugins:hibernate'

    provided 'org.postgresql:postgresql:9.4.1211.jre7'

    compile 'com.google.code.gson:gson:2.3.1'
    compile 'commons-lang:commons-lang:2.6'

    testCompile 'org.grails:grails-plugin-testing'

    provided 'org.springframework.boot:spring-boot-starter-logging'
    provided 'org.springframework.boot:spring-boot-starter-actuator'
    provided 'org.springframework.boot:spring-boot-autoconfigure'
    provided 'org.springframework.boot:spring-boot-starter-tomcat'

    provided 'org.grails:grails-web-boot'
    provided 'org.grails:grails-dependencies'
    provided 'javax.servlet:javax.servlet-api:3.1.0'

    console 'org.grails:grails-console'
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

bintray {
    pkg {
        userOrg = 'kaleidos' // If you want to publish to an organization
        name = "postgresql-extensions"
        issueTrackerUrl = "https://github.com/kaleidosnet/grails-grails-postgresql-extensions/issues"
        vcsUrl = "https://github.com/kaleidosnet/grails-grails-postgresql-extensions/issues"
    }
}

