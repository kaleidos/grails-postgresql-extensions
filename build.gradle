buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url 'https://repo.grails.org/grails/core' }
        mavenCentral()
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath 'org.grails.plugins:hibernate4:5.0.0'
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.7'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.2'
}

version '5.0.0'
group 'org.grails.plugins'

apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.grails.grails-plugin'
apply plugin: 'org.grails.grails-plugin-publish'
apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'


ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

cobertura {
    coverageExcludes = ['.*BootStrap.*', '.*test.*']
    coverageFormats = ['html', 'xml']
}

jar {
    exclude 'test/**'
    exclude 'UrlMappings.groovy'
    exclude 'error.gsp'
    exclude 'messages.properties'
    exclude 'hibernate/**'
    exclude 'layouts/**'
    exclude 'spring/**'

    doFirst {
        File pd = new File(buildDir, 'classes/main/META-INF/grails-plugin.xml')
        pd.text = pd.text.readLines().findAll { !it.contains('<resource>test.') }.join('\n')
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repo.grails.org/grails/core' }
}


dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    // Grails deps
    compile 'org.springframework.boot:spring-boot-starter-logging'
    compile 'org.springframework.boot:spring-boot-autoconfigure'
    compile 'org.grails:grails-core'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-tomcat'
    compile 'org.grails:grails-dependencies'
    compile 'org.grails:grails-web-boot'
    console 'org.grails:grails-console'
    profile 'org.grails.profiles:web-plugin:3.1.0'
    provided 'org.grails:grails-plugin-services'
    provided 'org.grails:grails-plugin-domain-class'
    testCompile 'org.grails:grails-plugin-testing'
    testCompile 'org.grails.plugins:geb'
    testRuntime 'org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1'
    testRuntime 'net.sourceforge.htmlunit:htmlunit:2.18'

    // plugins
    provided 'org.grails.plugins:hibernate4'
    provided 'org.hibernate:hibernate-ehcache'

    provided 'org.postgresql:postgresql:9.4.1211.jre7'
    // libraries
    provided 'org.postgresql:postgresql:9.4.1207'

    compile 'com.google.code.gson:gson:2.3.1'
    compile 'commons-lang:commons-lang:2.6'
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

bintray {
    pkg {
        userOrg = 'kaleidos' // If you want to publish to an organization
        name = 'postgresql-extensions'
        issueTrackerUrl = 'https://github.com/kaleidosnet/grails-grails-postgresql-extensions/issues'
        vcsUrl = 'https://github.com/kaleidosnet/grails-grails-postgresql-extensions/issues'
    }
}

grailsPublish {
    user = 'kaleidos'
    key = 'key'
    userOrg = 'kaleidos'
    repo = 'plugins'

    websiteUrl = 'http://foo.com/myplugin'
    license {
        name = 'Apache-2.0'
    }
    issueTrackerUrl = 'https://github.com/kaleidosnet/grails-grails-postgresql-extensions/issues'
    vcsUrl = 'https://github.com/kaleidos/grails-postgresql-extensions.git'
    title = 'PostgreSQL Extensions'
    desc = 'This is a grails plugin that provides hibernate user types to use Postgresql native types. It also provides new criterias to query this new native types.'
    developers = [kaleidos: 'Kaleidos']
}